:: 
:: siapp-sdk
::
:: SPDX-License-Identifier: MIT
:: Copyright 2020 Siemens AG
::
:: Authors:
::   Lukas Wimmer <lukas.wimmer@siemens.com>
::

@echo off
SETLOCAL

SET OUTPUT_DIRECTORY=build
SET powershell_cmd=powershell.exe

SET me=%~nx0
SET me_path=%~dp0


if "%~1" == "" (
   echo %me% - ERROR: project directory path not specified! 1>&2
   echo %me% - SYNOPSIS: run.bat PROJECTPATH [-name NAME] [-version VERSION]
   exit /b 1
)

set projectDirectoryPath=%~1

set projectName=%~nx1
set version=0.0.1

shift
:optionalArgumentsLoop
if not "%1" == "" (
   if "%1" == "-name" (
      set projectName=%2
      shift
   )
   if "%1" == "-version" (
      set version=%2
      shift
   )
   shift
   goto :optionalArgumentsLoop
)
@FOR /F %%s IN ('%powershell_cmd% -command "(get-item env:'projectName').Value.ToLower().replace(' ','')"') DO @set projectName=%%s

call :cleanup

call :getSimulationCSV

call :runSIMULATION
if %ERRORLEVEL% NEQ 0 (
   echo %me% - Error: Could not start data simulation container! Aborting... 1>&2
   exit /b %ERRORLEVEL%
)

call :runSIAPP
if %ERRORLEVEL% NEQ 0 (
   echo %me% - Error: Could not start siapp! Aborting... 1>&2
   exit /b %ERRORLEVEL%
)

timeout 2 > NUL
docker port emu

echo %me% - Successfully simulated %buildDirectoryPath%\%projectName%-%version%.siapp
exit /b 0

:runSIMULATION
   docker build -t sim templates/Simulation
   if %ERRORLEVEL% NEQ 0 (
      echo %me% - Error: Could not build simulation docker image! 1>&2
      exit /b %ERRORLEVEL%
   )
   START "Data Simulation" CMD /K "docker run --name=sim -v edgedata:/edgedata -it sim"
   exit /b 0

:runSIAPP
   if not exist "%projectDirectoryPath%\Dockerfile.arm32v7" (
      echo %me% - Error: File %projectDirectoryPath%\Dockerfile.arm32v7 does not exist! 1>&2
      exit /b 2
   )

   START "SIAPP EMULATION" CMD /K docker run --name=emu --rm --publish-all=true -v edgedata:/edgedata -it %projectName%-arm32v7-%version%
   if %ERRORLEVEL% NEQ 0 (
      echo %me% - Error: Could not start siapp! 1>&2
      exit /b %ERRORLEVEL%
   )
   exit /b 0
  
:getSimulationCSV
   if not exist "%projectDirectoryPath%\events.csv" (
      if not exist "%me_path%\templates\events.csv" (
         echo %me% - Error: Could not find events.csv template file %me_path%\templates\events.csv not found. 1>&2
         call :cleanup
         exit /b 3
         )
         echo %me% - Info: CSV file %projectDirectoryPath%\events.csv not found. CSV File is generated by template file! 1>&2
         @copy "%me_path%\templates\events.csv" "%projectDirectoryPath%\events.csv" 1>&2
      )
   if not exist "%projectDirectoryPath%\discover.csv" (
      if not exist "%me_path%\templates\discover.csv" (
         echo %me% - Error: Could not find discover.csv template file %me_path%\templates\discover.csv not found. 1>&2
         call :cleanup
         exit /b 3
         )
         echo %me% - Info: CSV file %projectDirectoryPath%\discover.csv not found. CSV File is generated by template file! 1>&2
         @copy "%me_path%\templates\discover.csv" "%projectDirectoryPath%\discover.csv" 1>&2
      )
   @copy "%projectDirectoryPath%\discover.csv" "%me_path%\templates\simulation\discover.csv" 1>&2
   @copy "%projectDirectoryPath%\events.csv" "%me_path%\templates\simulation\events.csv" 1>&2
   exit /b 0


:cleanup
   echo %me% - Remove all old containers 
   docker rm sim --force 1>&2
   docker rm emu --force 1>&2
   rem FOR /f "tokens=*" %%i IN ('docker ps -aq') DO docker rm %%i --force
   exit /b 0
